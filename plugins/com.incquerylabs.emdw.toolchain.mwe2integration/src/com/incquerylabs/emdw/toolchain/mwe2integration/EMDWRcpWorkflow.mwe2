/*******************************************************************************
 * Copyright (c) 2015-2016, IncQuery Labs Ltd. and Ericsson AB
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Akos Horvath, Abel Hegedus, Zoltan Ujhelyi, Peter Lunk - initial API and implementation
 *******************************************************************************/
module com.incquerylabs.emdw.toolchain.mwe2integration

import org.eclipse.viatra.emf.mwe2integration.*
import org.eclipse.viatra.emf.mwe2integration.mwe2impl.*
import com.incquerylabs.emdw.toolchain.mwe2integration.steps.*
import com.incquerylabs.emdw.toolchain.mwe2integration.*
import org.eclipse.xtext.xbase.lib.Pair.*

Workflow {
	component = PlainJavaInitializerComponent{
	}
	
	component = TransformationChain {
		step = XtTransformationStep {}
		
		step = XtChangeMonitorStep {
			publishing = PublishTo {topicName = "DirtyComponents" factory = messages.DirtyComponentsMessageFactory{}}
			publishing = PublishTo {topicName = "FileContentCreation" factory = messages.DirtyComponentsMessageFactory{}}
		}
		
		step = CppQrtTransformationStep {}
		
		step = CppComponentTransformationStep{
			subscription = SubscribeTo{topicName = "DirtyComponents" processor = messages.DirtyComponentsMessageProcessor{}}
		}
		
		step = FileContentCreationStep{
			subscription = SubscribeTo{topicName = "FileContentCreation" processor = messages.DirtyComponentsMessageProcessor{}}
			isRcpApp = true
		}
		
	}
}
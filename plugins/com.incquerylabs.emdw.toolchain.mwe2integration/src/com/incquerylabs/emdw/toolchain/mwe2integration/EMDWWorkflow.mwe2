module com.incquerylabs.emdw.toolchain.mwe2integration

import org.eclipse.viatra.emf.mwe2integration.*
import org.eclipse.viatra.emf.mwe2integration.mwe2impl.*
import com.incquerylabs.emdw.toolchain.mwe2integration.*

Workflow {
	component = InitializerComponent{
		codeGenerationDirectoryName = "src"
		generationProjectPrefix = "com.ericsson.emdw.cpp.generated.code."
		modelGenerationDirectoryName = "model"
		umlResourcePath =  "com.ericsson.emdw.tests.models/models/system-test/eatf.uml"
		primitiveTypesPath = "/org.eclipse.papyrusrt.xtumlrt.common.model/model/umlPrimitiveTypes.common"
	}
	
	component = TransformationChain {
		step = XtTransformationStep {}
		
		step = XtChangeMonitorStep {
			publishing = PublishTo {topicName = "DirtyComponents" factory = messages.DirtyComponentsMessageFactory{}}
			publishing = PublishTo {topicName = "FileContentCreation" factory = messages.DirtyComponentsMessageFactory{}}
		}
		
		step = CppQrtTransformationStep {}
		
		step = CppComponentTransformationStep{
			subscription = SubscribeTo{topicName = "DirtyComponents" processor = messages.DirtyComponentsMessageProcessor{}}
		}
		
		step = FileContentCreationStep{
			subscription = SubscribeTo{topicName = "FileContentCreation" processor = messages.DirtyComponentsMessageProcessor{}}
		}
		
	}
}
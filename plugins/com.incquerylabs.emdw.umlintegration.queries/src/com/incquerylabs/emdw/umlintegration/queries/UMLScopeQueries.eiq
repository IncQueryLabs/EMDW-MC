package com.incquerylabs.emdw.umlintegration.queries

import "http://www.eclipse.org/uml2/5.0.0/UML"

pattern signals(sig : Signal) {
    Signal(sig);
}

pattern umlTypes(type : Type) {
    Type(type);
}

pattern superClass(sub : Class, sup : Class) {
    find xtGeneralization(sub, gen);
    Generalization.target(gen, sup);
}

pattern attributesOfClass(cl : Class, attribute : Property) {
    Class.ownedAttribute(cl, attribute);
} or {
    find superClass+(cl, sup);
    Class.ownedAttribute(sup, attribute);
}

pattern associationsOfClass(cl : Class, association : Property) {
    find xtAssociation(_, sourceEnd, association);
    Property.type(sourceEnd, cl);
} or {
    find superClass+(cl, sup);
    find xtAssociation(_, sourceEnd, association);
    Property.type(sourceEnd, sup);
}

pattern operationsOfClass(cl : Class, op : Operation) {
    find operation(cl, op);
} or {
    find superClass+(cl, sup);
    find operation(sup, op);
}

pattern staticOperations(op : Operation) {
    find operation(_, op);
    Operation.isStatic(op, true);
}
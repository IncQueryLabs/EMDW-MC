package com.incquerylabs.emdw.xtuml.incquery

import "http://www.eclipse.org/papyrusrt/xtumlrt/xtuml"
import "http://www.eclipse.org/papyrusrt/xtumlrt/common"

@Constraint(
	key = {"trigger"},
	message = "Transition $transition$ has a trigger $trigger$ without signal",
	severity = "error"
)
pattern transitionTriggerWithoutSignal(transition : Transition, trigger : Trigger) {
	Transition.triggers(transition, trigger);
	neg find eventTriggerSignal(trigger, _);
}

pattern eventTriggerSignal(trigger : XTEventTrigger, signal : XTClassEvent) {
	XTEventTrigger.signal(trigger, signal);
}

@Constraint(
	key = {"namedElement1", "namedElement2"},
	message = "Multiple elements with name $namedElement1$ inside $baseContainer$",
	severity = "error",
	symmetric = {"namedElement1", "namedElement2"}
)
pattern cppNameCollision(
	baseContainer : BaseContainer,
	namedElement1 : NamedElement,
	namedElement2 : NamedElement
) {
	find containedElements(baseContainer, namedElement1);
	find containedElements(baseContainer, namedElement2);
	namedElement1 != namedElement2;
	NamedElement.name(namedElement1, name);
	NamedElement.name(namedElement2, name);
}

@Constraint(
	key = {"namedElement"},
	message = "Element with name $namedElement$ has the same name as its containing Package",
	severity = "error"
)
pattern fileNameCollision(
	umlPackage : Package,
	namedElement : NamedElement
) {
	Package.entities(umlPackage, namedElement);
	NamedElement.name(umlPackage, name);
	NamedElement.name(namedElement, name);
}

@Constraint(
	key = {"attribute"},
	message = "Attribute $attribute$ in structured type $structuredType$ has no type",
	severity = "error"
)
pattern attributeWithoutType(structuredType : StructuredType, attribute : Attribute) {
	StructuredType.attributes(structuredType, attribute);
	neg find typedMultiplicityElementWithType(attribute, _);
}

@Constraint(
	key = {"parameter"},
	message = "Parameter $parameter$ of signal $signal$ has no type",
	severity = "error"
)
pattern signalParameterWithoutType(signal : Signal, parameter : Parameter) {
	Signal.parameters(signal, parameter);
	neg find typedMultiplicityElementWithType(parameter, _);
}

@Constraint(
	key = {"parameter"},
	message = "Parameter $parameter$ of operation $operation$ in entity $entity$ has no type",
	severity = "error"
)
pattern operationParameterWithoutType(entity : Entity, operation : OperationSignature, parameter : Parameter) {
	Entity.operations(entity, operation);
	OperationSignature.parameters(operation, parameter);
	neg find typedMultiplicityElementWithType(parameter, _);
}

@Constraint(
	key = {"returnType"},
	message = "Return type of operation $operation$ is not set",
	severity = "error"
)
pattern operationWithUnsetReturnType(entity : Entity, operation : OperationSignature, returnType : TypedMultiplicityElement) {
	Entity.operations(entity, operation);
	OperationSignature.returnType(operation, returnType);
	neg find typedMultiplicityElementWithType(returnType, _);
}

private pattern typedMultiplicityElementWithType(
	element : TypedMultiplicityElement,
	type : Type
) {
	TypedMultiplicityElement.type(element, type);
}

pattern containedElements(
	baseContainer : BaseContainer,
	namedElement : NamedElement
) {
	BaseContainer.packages(baseContainer, namedElement);
} or {
	BaseContainer.entities(baseContainer, namedElement);
}
